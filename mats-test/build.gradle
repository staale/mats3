// mats-test

dependencies {
    // The Mats API
    api project(':mats-api')
    // The MatsFuturizer is provided as a convenience in the Rule_Mats and Extension_Mats
    api project(':mats-util')

    // We need an implementation of the Mats API
    implementation project(':mats-impl-jms')

    // Backing message brokers:
    // We need a backing JMS-based Message Broker; MatsTestBroker is a tool that provides easy instantiation of such.
    // (Note that the MatsTestBroker does NOT transitively depend on any broker server or client, as MatsTestBroker also
    // is pulled in by mats-spring-jms (which is meant for prod code) in conjunction with that toolset's readiness for
    // testing. Therefore, we are pulling in the message broker and client dependencies here: If you depend on the
    // 'mats-test' project, you are clearly inside a test-part of the project, not production. Also therefore, it does
    // not matter as much that we pull in a bit too much; We'll pull in both ActiveMQ and Artemis (both broker and
    // client), even though we'll probably only be using one of them, and that is probably ActiveMQ due to the test
    // tooling defaulting to this.)
    implementation project(':mats-test-broker')

    // For extension Extension_MatsClassMapping, we need to include the spring dependencies. We expect anyone that
    // uses that extension to also provide mats-spring and the SpringContext in their projects dependencies.
    compileOnly project(":mats-spring")
    compileOnly "org.springframework:spring-context:$springVersion"


    // .. therefore, also pull in ActiveMQ Broker with Client
    implementation "org.apache.activemq:activemq-broker:$activeMqVersion"
    // .. and pull in The Artemis (Apache ActiveMQ Artemis) Client and Server, since you can choose to test with it.
    implementation "org.apache.activemq:artemis-jms-client:$artemisMqVersion"
    implementation "org.apache.activemq:artemis-server:$artemisMqVersion"

    // H2 Database, for database tests - but we do not want to make this dependency transitive, so compileOnly.
    compileOnly "com.h2database:h2:$h2Version"

    // === TEST
    testImplementation "junit:junit:$junitVersion"
}

// To get output from the Test_CI_Variable "test"
test {
    testLogging {
        showStandardStreams = true
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = 'Mats^3 Testing tooling'
                description = 'Mats^3 Testing tools, as well as the base for specific JUnit or Jupiter (Junit 5) tools.'
            }
        }
    }
}